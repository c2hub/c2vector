module c2vector;

import stdlib local;
import stdio local;

//Implementation of a Vector in C2. Based on tutorial for
//C by Edd Mann
public type Vector struct
{
    void** items;
    uint32 capacity;
    int32 total_;
}

public func void Vector.init(Vector* v)
{
    v.capacity = 4;
    v.total_ = 0;
    v.items = malloc(sizeof(void *) * v.capacity);
}

public func int32 Vector.total(Vector* v)
{
    return v.total_;
}

func void resize(Vector* v, uint32 capacity)
{
    void** items = realloc(v.items, sizeof(void *) * capacity);
    if (items != nil)
	{
        v.items = items;
        v.capacity = capacity;
    }
}

public func void Vector.add(Vector* v, void* item)
{
    if (v.capacity == v.total_)
        resize(v, v.capacity * 2);
    v.items[v.total_++] = item;
}

public func void Vector.set(Vector* v, int32 index, void* item)
{
    if (index >= 0 && index < v.total_)
        v.items[index] = item;
}

public func void* Vector.get(Vector* v, int32 index)
{
    if (index >= 0 && index < v.total_)
        return v.items[index];
    return nil;
}

public func void Vector.delete(Vector* v, int32 index)
{
    if (index < 0 || index >= v.total_)
        return;

    v.items[index] = nil;

    for (int32 i = index; i < v.total_ - 1; i++)
	{
        v.items[i] = v.items[i + 1];
        v.items[i + 1] = nil;
    }

    v.total_--;

    if (v.total_ > 0 && v.total_ == v.capacity / 4)
        resize(v, v.capacity / 2);
}

public func void Vector.free(Vector *v)
{
    stdlib.free(v.items);
}
